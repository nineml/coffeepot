buildscript {
  repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://dev.saxonica.com/maven" }
  }

  configurations.all {
    resolutionStrategy {
      force 'xml-apis:xml-apis:1.4.01',
        "${saxonGroup}:${saxonEdition}:${saxonVersion}",
        "org.xmlresolver:xmlresolver:${xmlresolverVersion}",
        'com.nwalsh:sinclude:4.0.0'
    }
  }

  dependencies {
    classpath group: saxonGroup, name: saxonEdition, version: saxonVersion
    classpath group: 'org.docbook', name: 'schemas-docbook', version: docbookVersion
    classpath group: 'org.docbook', name: 'docbook-xslTNG', version: xslTNGversion
  }
}

plugins {
  id "java"
  id "maven-publish"
  id "signing"
  id 'com.github.gmazzo.buildconfig' version "3.0.3"
  id 'com.nwalsh.gradle.saxon.saxon-gradle' version '0.9.3'
  id 'com.nwalsh.gradle.relaxng.validate' version '0.0.8'
  id 'com.nwalsh.gradle.docker.container' version '0.0.3'
}

import java.text.DateFormat
import java.text.SimpleDateFormat
import com.nwalsh.gradle.saxon.SaxonXsltTask
import com.nwalsh.gradle.relaxng.validate.RelaxNGValidateTask
import com.nwalsh.gradle.docker.DockerContainer

repositories {
  mavenLocal()
  mavenCentral()
  maven { url "https://dev.saxonica.com/maven" }
}

configurations.all {
  resolutionStrategy {
    force 'xml-apis:xml-apis:1.4.01',
      "${saxonGroup}:${saxonEdition}:${saxonVersion}",
      "org.xmlresolver:xmlresolver:${xmlresolverVersion}",
      'com.nwalsh:sinclude:4.0.0'
  }
}

configurations {
  transform.extendsFrom documentation
  app.extendsFrom implementation
}

dependencies {
  implementation (
    [group: 'org.nineml', name: grinderName, version: grinderVersion],
    [group: 'org.nineml', name: filterName, version: filterVersion],
    [group: 'com.beust', name: 'jcommander', version: '1.81' ],
    [group: 'org.fusesource.jansi', name: 'jansi', version: '2.4.0'],
    [group: 'net.sf.saxon', name: 'Saxon-HE', version: saxonVersion]
  )
  testImplementation (
    [group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.4.2'],
    [group: 'org.junit.platform', name: 'junit-platform-console-standalone', version: '1.7.1'],
  )
  testRuntimeOnly (
    [group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.4.2'],
    [group: 'org.junit.vintage', name: 'junit-vintage-engine', version: '5.4.2'],
    [group: 'org.junit.platform', name: 'junit-platform-runner', version: '1.8.2']
  )
  documentation (
    [group: saxonGroup, name: saxonEdition, version: saxonVersion],
    [group: 'org.docbook', name: 'schemas-docbook', version: docbookVersion],
    [group: 'org.docbook', name: 'docbook-xslTNG', version: xslTNGversion]
  )
}

ext {
  c_coffeepot = "coffeepot"
}

saxon.configure {
  classpath configurations.transform
  initializer 'org.docbook.xsltng.extensions.Register'
}

docker_container.configure {
  workingDir = "docker"
}

tasks.withType(Test) {
  useJUnitPlatform()
}

public String dateTime() {
  TimeZone tz = TimeZone.getTimeZone("UTC");
  DateFormat df = new SimpleDateFormat("yyyy-MM-dd'T'HH:mm:ss'Z'");
  df.setTimeZone(tz);
  return df.format(new Date());
}

public String gitHash() {
  def hashout = new ByteArrayOutputStream()
  exec {
    commandLine "git", "log", "-1", "--format=%h"
    standardOutput = hashout
  }
  return hashout.toString().trim()
}

// ============================================================

buildConfig {
  packageName('org.nineml.coffeepot')
  buildConfigField('String', 'TITLE', "\"${potTitle}\"")
  buildConfigField('String', 'VERSION', "\"${potVersion}\"")
  buildConfigField('String', 'FILTER_VERSION', "\"${filterVersion}\"")
  buildConfigField('String', 'GRINDER_VERSION', "\"${grinderVersion}\"")
  buildConfigField('String', 'PUB_DATE', "\"${dateTime()}\"")
  buildConfigField('String', 'PUB_HASH', "\"${gitHash()}\"")
}

// ============================================================

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:deprecation'
}

public String jarClasspath() {
  StringBuilder sb = new StringBuilder()
  String fsep = System.getProperty("file.separator")
  sb.append("lib")
  configurations.app.collect { dep ->
    String jar = dep.toString()
    int pos = jar.lastIndexOf(fsep)
    jar = jar.substring(pos)
    sb.append(" ").append("lib").append(jar)
  }
  return sb.toString()
}

jar {
  setDuplicatesStrategy(DuplicatesStrategy.WARN)
  manifest {
    attributes "Project-Name": "${potTitle}"
    attributes 'Built-By': 'Norman Walsh'
    attributes 'Implementation-Vendor': 'Norman Walsh'
    attributes 'Implementation-Title': potTitle
    attributes 'Implementation-Version': potVersion
    attributes "Main-Class": "org.nineml.coffeepot.Main"
    attributes "Class-Path": jarClasspath()
  }
  archiveBaseName = "${potName}-${potVersion}"
}

task copyRuntimeLibraries() {
  doLast {
    configurations.app.collect { dep ->
      copy {
        into "${buildDir}/libs/lib"
        from dep
      }
    }
  }
}
jar.finalizedBy(copyRuntimeLibraries)

task dist(dependsOn: "jar") {
  doLast {
    mkdir "${buildDir}/stage"
    mkdir "${buildDir}/stage/docs"
  }
  doLast {
    copy {
      from "${buildDir}/libs"
      into "${buildDir}/stage"
    }
  }
  doLast {
    copy {
      from "${projectDir}/README.md"
      into "${buildDir}/stage/docs"
    }
  }
}

task zipDist(type: Zip) {
  from "${buildDir}/stage"
  into "${potName}-${potVersion}"
  archiveFileName = "${potName}-${potVersion}.zip"
}
dist.finalizedBy zipDist

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

task sourcesJar(type: Jar, dependsOn: ['generateBuildConfig']) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

// ============================================================

task manual_xinclude(type: SaxonXsltTask, dependsOn: ["copyResources"]) {
  inputs.files fileTree(dir: "${projectDir}/src/website/xml")
  inputs.file "${projectDir}/website/xinclude.xsl"

  input "${projectDir}/src/website/xml/coffeepot.xml"
  stylesheet "${projectDir}/website/xinclude.xsl"
  output "${buildDir}/tmp/coffeepot.xml"
}

task manual_validate(type: RelaxNGValidateTask, dependsOn: ["manual_xinclude"]) {
  input manual_xinclude.outputs.files.singleFile
  output "${buildDir}/xml/coffeepot.xml"
  schema "${projectDir}/website/docbook.rnc"
  compact true
}

task manual_augment(type: SaxonXsltTask, dependsOn: ["manual_validate"]) {
  input manual_validate.outputs.files.singleFile
  output "${buildDir}/aug/coffeepot.xml"
  stylesheet "${projectDir}/tools/augment.xsl"
  parameters (
    'version': potVersion,
    'coffeegrinder-version': grinderVersion,
    'coffeefilter-version': filterVersion
  )
}

task website(type: SaxonXsltTask, dependsOn: ["manual_augment"]) {
  inputs.file "${projectDir}/tools/docbook.xsl"
  inputs.file "${projectDir}/website/docbook.xsl"
  outputs.files fileTree(dir: "${buildDir}/website")

  input manual_augment.outputs.files.singleFile
  output "${buildDir}/website/index.html"
  stylesheet "${projectDir}/tools/docbook.xsl"
  initializer 'org.docbook.xsltng.extensions.Register'
  parameters (
    'mediaobject-input-base-uri': "file:${buildDir}/aug/",
    'mediaobject-output-base-uri': "/",
    'chunk': 'index.html',
    'chunk-output-base-uri': "${projectDir}/build/website/"
  )
}

task copyResources(dependsOn: ["copyJarResources", "copyStaticResources"]) {
  // nop
}

task copyJarResources() {
  outputs.files fileTree(dir: "${buildDir}/website")

  def dbjar = null
  configurations.transform.each { path ->
    if (path.toString().contains("docbook-xslTNG")) {
      dbjar = path
    }
  }

  doLast {
    if (dbjar == null) {
      throw new GradleException("Failed to locate DocBook xslTNG jar file")
    }
    copy {
      into "${buildDir}/website"
      from ({ zipTree(dbjar.toString()) }) {
        include "org/docbook/xsltng/resources/**"
      }
      eachFile { fileCopyDetails ->
        def originalPath = fileCopyDetails.path
        fileCopyDetails.path = originalPath.replace('org/docbook/xsltng/resources/', '')
      }
    }
  }

  doLast {
    delete "${buildDir}/website/org"
  }
}

task copyStaticResources() {
  inputs.files fileTree(dir: "${projectDir}/src/website/resources")
  inputs.files fileTree(dir: "${projectDir}/website/resources")
  inputs.files fileTree(dir: "${projectDir}/src/website/xml")
  outputs.files fileTree(dir: "${buildDir}/website")
  outputs.files fileTree(dir: "${buildDir}/aug")

  doLast {
    copy {
      into "${buildDir}/website"
      from "${projectDir}/src/website/resources"
    }
  }

  doLast {
    copy {
      into "${buildDir}/website"
      from "${projectDir}/website/resources"
    }
  }

  doLast {
    copy {
      from "${projectDir}/src/website/xml/images"
      into "${buildDir}/website/images"
    }
  }

  // FIXME: Grotesque hack. 
  doLast {
    copy {
      from "${projectDir}/src/website/xml/images"
      into "${buildDir}/aug/images"
    }
  }

  // FIXME: Grotesque hack. 
  doLast {
    copy {
      from "${projectDir}/src/website/resources/images"
      into "${buildDir}/aug/images"
    }
  }

  doLast {
    copy {
      from "${projectDir}/src/website/xml/examples"
      into "${buildDir}/website/examples"
    }
  }
}

// ============================================================

signing {
  sign publishing.publications
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      pom {
        name = potTitle
        packaging = 'jar'
        description = 'An Invisible XML processor'
        url = 'https://github.com/nineml/coffeepot'

        scm {
          url = 'scm:git@github.com:nineml/coffeepot.git'
          connection = 'scm:git@github.com:nineml/coffeepot.git'
          developerConnection = 'scm:git@github.com:nineml/coffeepot.git'
        }

        licenses {
          license {
            name = 'Mozilla Public License Version 2.0'
            url = 'http://www.mozilla.org/MPL/2.0/'
            distribution = 'repo'
          }
        }

        developers {
          developer {
            id = 'ndw'
            name = 'Norman Walsh'
          }
        }
      }

      groupId = "org.nineml"
      artifactId = potName
      version = potVersion
      from components.java
      artifact javadocJar
      artifact sourcesJar
    }
  }

  repositories {
    maven {
      url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
      credentials {
        username = project.findProperty("sonatypeUsername") ?: "NOUSER"
        password = project.findProperty("sonatypePassword") ?: "NOPASS"
      }
    }
  }
}

// ======================================================================

task dockerup() {
  doLast {
    if (!DockerContainer.running(c_coffeepot)) {
      DockerContainer.compose {
        command = "up"
        options = ["-p", "coffeepot"]
        args = ["-d"]
      }
    }
  }
}

task dockerdown() {
  doLast {
    if (DockerContainer.running(c_coffeepot)) {
      DockerContainer.compose {
        command = "down"
        options = ["-p", "coffeepot"]
      }
    }
  }
}

// ============================================================

task helloWorld() {
  doLast {
    println('Hello, world.')
  }
}
